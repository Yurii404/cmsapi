plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.diffplug.spotless' version '6.8.0'
}

ext {
    set('springCloudVersion', "2022.0.4")
    set('springBootVersion', "3.0.1")
}

allprojects {
    group = 'com.sombra.cmsapi'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            googleJavaFormat("1.18.1")
        }
    }

    compileJava.dependsOn 'spotlessApply'

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        implementation 'mysql:mysql-connector-java:8.0.33'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
                    exclude group: 'org.mockito', module: 'mockito-core'
        }
        testImplementation 'org.mockito:mockito-inline:4.6.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    }

    test {
        useJUnitPlatform()
    }
}
